filetype off
set runtimepath+=~/.vim/bundle/vundle/
call vundle#rc()

let mapleader = "\<Space>"
let maplocalleader = "\<Space>"

" Plugins {{{1
Plugin 'gmarik/vundle'

Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'tpope/vim-sensible'
Plugin 'tpope/vim-repeat'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-fugitive'
Plugin 'tpope/vim-rsi'
Plugin 'tpope/vim-unimpaired'
Plugin 'ajh17/VimCompletesMe'
Plugin 'scrooloose/syntastic'
Plugin 'jeetsukumaran/vim-filebeagle'
Plugin 'AndrewRadev/splitjoin.vim'
Plugin 'tommcdo/vim-lion'
Plugin 'justinmk/vim-sneak'
Plugin 'majutsushi/tagbar'
Plugin 'derekwyatt/vim-fswitch'
Plugin 'joom/latex-unicoder.vim'
Plugin 'idanarye/vim-merginal'

Plugin 'kana/vim-textobj-user'
Plugin 'kana/vim-operator-user'
Plugin 'kana/vim-textobj-indent'
Plugin 'kana/vim-textobj-diff'
Plugin 'kana/vim-operator-replace'
Plugin 'wellle/targets.vim'
Plugin 'tommcdo/vim-ninja-feet'
Plugin 'nelstrom/vim-textobj-rubyblock'
Plugin 'Julian/vim-textobj-variable-segment'
Plugin 'beloglazov/vim-textobj-quotes'
Plugin 'jeetsukumaran/vim-indentwise'
Plugin 'gcmt/wildfire.vim'

Plugin 'tpope/vim-git'
Plugin 'aliva/vim-fish'
Plugin 'othree/html5.vim'
Plugin 'pangloss/vim-javascript'
Plugin 'kchmck/vim-coffee-script'
Plugin 'vim-scripts/VimClojure'
Plugin 'derekwyatt/vim-scala'
Plugin 'vim-ruby/vim-ruby'
Plugin 'hynek/vim-python-pep8-indent'
Plugin 'slim-template/vim-slim'

Plugin 'sjl/badwolf'
Plugin 'sickill/vim-monokai'

let g:sneak#streak = 1

map gr <Plug>(operator-replace)

nnoremap ys<space> ysl<space><space>

nnoremap <silent> <leader>gs :Gstatus<CR>
nnoremap <silent> <leader>gl :Glog<CR>
nnoremap <silent> <leader>gc :Gcommit<CR>
nnoremap <silent> <leader>gd :Gdiff<CR>
nnoremap <silent> <leader>gw :Gwrite<CR>
nnoremap <silent> <leader>gr :Gread<CR>
nnoremap <silent> <leader>ge :Gedit<CR>
nnoremap <silent> <leader>gg :Ggrep <C-r><C-w><CR>

nnoremap <leader>t :TagbarOpenAutoClose<CR>

let g:ctrlp_cache_dir = $HOME.'/.cache/ctrlp'
if has('win32')
    let g:ctrlp_cache_dir = $TEMP.'/ctrlp'
endif
let g:ctrlp_user_command = {
    \ 'types': {
        \ 1: ['.git', 'cd %s && git ls-files . -co --exclude-standard'],
        \ 2: ['.hg', 'hg --cwd %s status -numac -I . $(hg root)'],
    \ },
    \ 'fallback': 'find %s -type f',
    \ }
let g:ctrlp_use_caching = 0

let g:syntastic_mode_map = { 'mode': 'active',
                           \ 'active_filetypes': [],
                           \ 'passive_filetypes': ['java'] }
let g:syntastic_ruby_checkers = ['mri', 'rubocop']
let g:syntastic_cpp_compiler_options = '-std=c++11'

let g:unicoder_cancel_normal = 1
" }}}1

" Options {{{1
set cpoptions+=J

set termencoding=utf-8
set fileencodings=utf-8,windows-1251,iso-8859-15,koi8-r
set encoding=utf-8

set novisualbell t_vb=
set title
set ttyfast
set lazyredraw
set mousehide
set hidden
set virtualedit=all
set nojoinspaces
set formatoptions-=t  " Do not auto-wrap text using textwidth
set formatoptions+=c  " Auto-wrap comments using textwidth
set formatoptions+=r  " Insert comment leader on Enter in insert mode
set formatoptions-=o  " Do not continue comments when pressing o/O
set formatoptions+=q  " Allow gq on comments
set formatoptions+=l  " Don't break long lines if it was long when entering insert mode
set formatoptions+=1  " Don't break after 1-letter word, break before it
set formatoptions+=j  " Remove comment leader wehn joining lines
set shellslash
set autowrite

set scrolljump=1
set scrolloff=5
set sidescrolloff=10
set sidescroll=10

set hlsearch
set wrapscan
set ignorecase
set smartcase
set noinfercase
set gdefault

set smartindent
set shiftwidth=2
set softtabstop=2
set tabstop=2
set expandtab

set nowrap
set showmatch
set list
set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
if !has('win32') && (&termencoding ==# 'utf-8' || &encoding ==# 'utf-8')
    let &listchars = "tab:\u21e5 ,trail:\u2423,extends:\u21c9,precedes:\u21c7,nbsp:\u00b7"
endif

if exists('&colorcolumn')
    set colorcolumn=80
endif

set foldmethod=indent
set foldlevel=20

set wildcharm=<TAB>
set wildmode=list:longest

set wildignore+=.git,.hg,_darcs,.svn
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest
set wildignore+=*.pyc,*.pyo
set wildignore+=*.hi
set wildignore+=*.luac
set wildignore+=*.aux,*.out,*.toc
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg
set wildignore+=*.spl
set wildignore+=*.sw?
set wildignore+=*.DS_Store?
set wildignore+=*.mo
set wildignore+=*.class
set wildignore+=node_modules
set wildignore+=.env

set nobackup
set directory=$HOME/.vim/tmp//
set viminfo='128,/32,:32,<64,@32,s10,h,n$HOME/.vim/viminfo
set sessionoptions=blank,buffers,curdir,slash,tabpages,unix
set cryptmethod=blowfish

set diffopt+=iwhite

set splitright
set splitbelow

set fillchars=""  " Get rid of the silly characters in window separators
set cmdheight=2
set cmdwinheight=3

set statusline=%*\ %f\ %<
set statusline+=%1*%m%*
set statusline+=%1*%r%*
set statusline+=%=
set statusline+=\ %{&fileformat}\ %{&fileencoding}
set statusline+=\ %{strlen(&filetype)>0?&filetype:'no-ft'}
set statusline+=\ %04b/0x%03B
set statusline+=\ %03vx%3*%03l%*/%03L
set statusline+=%*

set numberwidth=3
set number
if exists('&relativenumber')
    set norelativenumber
endif

if has('gui_running') " {{{2
    set mouse=a
    set mousemodel=popup

    set guioptions-=T  " No toolbar
    set guioptions-=l  " No scrollbars
    set guioptions-=r
    set guioptions-=L
    set guioptions-=R
    set guioptions-=m  " No menu

    set guicursor=n-v-c:block-Cursor-blinkon0
    set guicursor+=ve:ver35-Cursor
    set guicursor+=o:hor50-Cursor
    set guicursor+=i-ci:ver25-Cursor
    set guicursor+=r-cr:hor20-Cursor
    set guicursor+=sm:block-Cursor-blinkwait175-blinkoff150-blinkon17

    set guifont=Ubuntu\ Mono
endif " }}}2

set synmaxcol=512
" }}}1

" Mappings {{{1

" Navigate by screen lines, not real lines.
nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk

" Very magic search by default.
nnoremap / /\v

" Y behaves more like D, i.e. yanks to the end of line.
nnoremap Y y$

" Center screen on search match.
nnoremap n nzzzv
nnoremap N Nzzzv

" Select just pasted text.
nnoremap gp V`]

" Paste in visual mode without losing register content.
xnoremap <expr> p v:register=='"'?'pgvy':'p'

" Write as sudo
cnoremap w!! w !sudo tee % >/dev/null

" Highlight all instances of the current word under the cursor
nnoremap <silent> <leader>hh :setl hls<CR>:let @/="\\<<C-r><C-w>\\>"<CR>

" Quick vimrc editing.
nnoremap <leader>ev :vsplit $MYVIMRC<cr>
nnoremap <leader>rv :source $MYVIMRC<cr>

nnoremap <leader>w :w<CR>

" Good vacant mappings.
nnoremap U <nop>
nnoremap K <nop>
nnoremap <Del> <nop>
" nnoremap <CR> <nop>
" nnoremap _ <nop>
" nnoremap \| <nop>
" }}}1

" Autocommands {{{1
if has("autocmd")
    augroup FTOptions " {{{2
        autocmd!
        autocmd FileType vim setl fdm=marker sw=4 sts=4 ts=4
        autocmd FileType mail setl wrap
        autocmd FileType gitcommit setl spell

        autocmd FileType css,scss,sass,stylus setl sw=2 sts=2 ts=2 et nosi
        autocmd FileType cpp setl sw=4 sts=4 ts=4 et
        autocmd FileType fish setl sw=4 sts=4 ts=4 et
        autocmd FileType go setl sw=4 sts=4 ts=4 noet
        autocmd FileType haskell setl sw=4 sts=4 ts=4 et
        autocmd FileType html,htmldjango,liquid setl sw=2 sts=2 ts=2 et
        autocmd FileType lisp setl cms=;;%s
        autocmd FileType python setl sw=4 sts=4 ts=4 et nosi
        autocmd FileType ruby setl sw=2 sts=2 ts=2 et nosi
        autocmd FileType rust setl sw=4 sts=4 ts=4 et nosi
    augroup END " }}}2

    augroup Misc " {{{2
        autocmd!
        autocmd FocusLost * :wa
        autocmd VimResized * exe "normal! \<c-w>="
    augroup END " }}}2

    augroup Whitespace " {{{2
        autocmd!
        " Highlight EOL whitespace, http://vim.wikia.com/wiki/Highlight_unwanted_spaces
        autocmd ColorScheme * highlight ExtraWhitespace ctermbg=darkred guibg=#382424
        autocmd BufWinEnter * match ExtraWhitespace /\s\+$/
        " the above flashes annoyingly while typing, be calmer in insert mode
        autocmd InsertLeave * match ExtraWhitespace /\s\+$/
        autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/
    augroup END " }}}2

    augroup command_window
        autocmd!
        " Have <Ctrl-C> leave cmdline-window.
        autocmd CmdwinEnter * nnoremap <buffer> <C-c> :q\|echo ""<cr>
        autocmd CmdwinEnter * inoremap <buffer> <C-c> <esc>:q\|echo ""<cr>
        " Start command line window in insert mode and no line numbers.
        autocmd CmdwinEnter * startinsert
        autocmd CmdwinEnter * setl nonumber
        autocmd BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal g'\"" | endif
    augroup END
end
" }}}1

" Commands {{{1

" Visual star {{{
function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>
" }}}
" Fix whitespace {{{
highlight ExtraWhitespace ctermbg=darkred guibg=#382424

function! s:FixWhitespace(line1,line2)
    let l:save_cursor = getpos(".")
    silent! execute ':' . a:line1 . ',' . a:line2 . 's/\s\+$//'
    call setpos('.', l:save_cursor)
endfunction

" Run :FixWhitespace to remove end of line white space.
command! -range=% FixWhitespace call <SID>FixWhitespace(<line1>,<line2>)
" }}}

" }}}

" Abbreviations {{{1
function! EatChar(pat)
    let c = nr2char(getchar(0))
    return (c =~ a:pat) ? '' : c
endfunction

function! MakeSpacelessIabbrev(from, to)
    execute "iabbrev <silent> ".a:from." ".a:to."<C-R>=EatChar('\\s')<CR>"
endfunction

call MakeSpacelessIabbrev('***', '*args, **kwargs')
call MakeSpacelessIabbrev('pdb', 'import pdb; pdb.set_trace()')

iab wekk week
iab Wekk Week
iab chage change
iab Chage Change
iab reutrn return
iab reutn return
iab reurn return
iab labmda lambda
iab consoel console
" }}}

if filereadable($HOME."/.vim/local/vimrc")
    set runtimepath+=$HOME/.vim/local/
    source $HOME/.vim/local/vimrc
endif
