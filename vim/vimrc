let mapleader = "\<Space>"
let maplocalleader = "\<Space>"

" Plugins {{{1
let g:filebeagle_suppress_keymaps = 1

let g:ale_sign_column_always = 1
let g:ale_fix_on_save = 1
let g:ale_linters = {
\   'python': ['flake8', 'mypy'],
\}
let g:ale_fixers = {
\   '*': ['remove_trailing_lines', 'trim_whitespace'],
\   'python': ['black', 'isort'],
\}
let g:ale_python_mypy_ignore_invalid_syntax = 1

set runtimepath+=$HOME/.config/vim/
call plug#begin('~/.config/vim/bundles')

" Objects, motions, operators and such
Plug 'kana/vim-textobj-user'
Plug 'kana/vim-operator-user'
Plug 'wellle/targets.vim'
Plug 'Julian/vim-textobj-variable-segment'
Plug 'svermeulen/vim-NotableFt'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-commentary'

" Filetypes
Plug 'tpope/vim-git'
Plug 'hashivim/vim-terraform'
Plug 'Vimjas/vim-python-pep8-indent'

" File management
Plug 'junegunn/fzf'
Plug 'junegunn/fzf.vim'
Plug 'jeetsukumaran/vim-filebeagle'

" Code intel
Plug 'tpope/vim-fugitive'
Plug 'dense-analysis/ale'

" Colors
Plug 'morhetz/gruvbox'

call plug#end()

set background=dark
colorscheme gruvbox

nmap <silent> - <Plug>FileBeagleOpenCurrentBufferDir

nnoremap <silent> <leader>f :GFiles<CR>
nnoremap <silent> <leader>: :Commands<CR>

nnoremap <silent> <leader>gs :Git<CR>
nnoremap <silent> <leader>gd :Gdiffsplit<CR>
nnoremap <silent> <leader>gr :Gread<CR>
nnoremap <silent> <leader>ge :Gedit<CR>
nnoremap <silent> <leader>gg :Ggrep <C-r><C-w><CR>

nnoremap <silent> <leader>af :ALEFix<CR>
nnoremap <silent> <leader>ad :ALEGoToDefinition<CR>
" }}}1

" Options {{{1
set nomodeline  " modeline is a security issue

set termencoding=utf-8
set fileencodings=utf-8,windows-1251,iso-8859-15,koi8-r
set encoding=utf-8

set novisualbell t_vb=
set title
set ttyfast
set lazyredraw
set mousehide
set hidden
set virtualedit=all
set nojoinspaces
set formatoptions-=t  " Do not auto-wrap text using textwidth
set formatoptions+=c  " Auto-wrap comments using textwidth
set formatoptions+=r  " Insert comment leader on Enter in insert mode
set formatoptions-=o  " Do not continue comments when pressing o/O
set formatoptions+=q  " Allow gq on comments
set formatoptions+=l  " Don't break long lines if it was long when entering insert mode
set formatoptions+=1  " Don't break after 1-letter word, break before it
set formatoptions+=j  " Remove comment leader when joining lines
set shellslash
set autoread
set autowrite

set scrolljump=1
set scrolloff=5
set sidescrolloff=10
set sidescroll=10

set incsearch
set hlsearch
set wrapscan
set ignorecase
set smartcase
set noinfercase
set gdefault

set autoindent
set smartindent
set shiftwidth=2
set softtabstop=2
set tabstop=2
set expandtab
set smarttab

set backspace=indent,eol,start
set complete-=i

set nowrap
set showmatch
set list
set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
if !has('win32') && (&termencoding ==# 'utf-8' || &encoding ==# 'utf-8')
    let &listchars = "tab:\u21e5 ,trail:\u2423,extends:\u21c9,precedes:\u21c7,nbsp:\u00b7"
endif

if exists('&colorcolumn')
    set colorcolumn=80
endif

set foldmethod=indent
set foldlevel=20

filetype plugin indent on

syntax enable

set wildmenu
set wildcharm=<TAB>
set wildmode=list:longest

set wildignore+=.git,.hg,_darcs,.svn
set wildignore+=*.pyc,*.pyo,__pycache__
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest
set wildignore+=*.hi
set wildignore+=*.luac
set wildignore+=*.aux,*.out,*.toc
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg
set wildignore+=*.spl
set wildignore+=*.sw?
set wildignore+=*.DS_Store?
set wildignore+=*.mo
set wildignore+=*.class
set wildignore+=node_modules
set wildignore+=.env

set nobackup
set directory=$HOME/.cache/vim/swap/
set viminfo='128,/32,:32,<64,@32,s10,h,n$HOME/.cache/vim/viminfo
set sessionoptions=blank,buffers,curdir,slash,tabpages,unix
set cryptmethod=blowfish

set diffopt+=iwhite

set splitright
set splitbelow

set fillchars=""  " Get rid of the silly characters in window separators
set cmdheight=1
set cmdwinheight=3

set laststatus=2

set statusline=%*\ %f\ %<
set statusline+=%1*%m%*
set statusline+=%1*%r%*
set statusline+=%=
set statusline+=\ %{&fileformat}\ %{&fileencoding}
set statusline+=\ %{strlen(&filetype)>0?&filetype:'no-ft'}
set statusline+=\ %04b/0x%03B
set statusline+=\ %03vx%3*%03l%*/%03L
set statusline+=%*

set numberwidth=3
set number
if exists('&relativenumber')
    set norelativenumber
endif

if has('gui_running') " {{{2
    set mouse=a
    set mousemodel=popup

    set guioptions-=T  " No toolbar
    set guioptions-=l  " No scrollbars:
    set guioptions-=L  "   left
    set guioptions-=r  "   and
    set guioptions-=R  "   right
    set guioptions-=m  " No menu

    set guicursor=n-v-c:block-blinkon0-Cursor
    set guicursor+=ve:ver35-Cursor
    set guicursor+=o:hor50-Cursor
    set guicursor+=i-ci:ver25-Cursor
    set guicursor+=r-cr:hor20-Cursor
    set guicursor+=sm:block-blinkwait175-blinkoff150-blinkon17-Cursor

    set guifont=Iosevka-SS14-Extended:h15
endif " }}}2

if has('gui_macvim')
    set macligatures
endif

set synmaxcol=512

syn match NBSP '\%xa0' conceal cchar=␣
syn match NarrowNBSP '\%u202F' conceal cchar=⸤
" }}}1

" Mappings {{{1

" Navigate by screen lines, not real lines.
nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk

" Very magic search by default.
nnoremap / /\v

" Y behaves more like D, i.e. yanks to the end of line.
nnoremap Y y$

" Select just pasted text.
nnoremap gp V`]

" Paste in visual mode without losing register content.
xnoremap <expr> p v:register=='"'?'pgvy':'p'

" Write as sudo
cnoremap w!! w !sudo tee % >/dev/null

" Delete current file
cnoremap rm! call delete(expand('%:p')) \| bdelete!

nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>

" Highlight all instances of the current word under the cursor
nnoremap <silent> <leader>hh :setl hls<CR>:let @/="\\<<C-r><C-w>\\>"<CR>

" Quick vimrc editing.
nnoremap <leader>ev :vsplit $MYVIMRC<cr>
nnoremap <leader>rv :source $MYVIMRC<cr>

nnoremap <leader>w :w<CR>
nnoremap <leader>q :q<CR>

nnoremap <Backspace> <C-W>w

" Accept autocomplete by enter and tab.
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
inoremap <expr> <Tab> pumvisible() ? "\<C-y>" : "<C-p>"

" vim-unimpaired mappings I use
function! s:Context(reverse) abort
  call search('^\(@@ .* @@\|[<=>|]\{7}[<=>|]\@!\)', a:reverse ? 'bW' : 'W')
endfunction

nnoremap <silent> [q :cprev<CR>
nnoremap <silent> ]q :cnext<CR>
nnoremap <silent> [l :lprev<CR>
nnoremap <silent> ]l :lnext<CR>
nnoremap <silent> [t :tprev<CR>
nnoremap <silent> ]t :tnext<CR>
nnoremap <silent> [n :<C-U>call <SID>Context(1)<CR>
nnoremap <silent> ]n :<C-U>call <SID>Context(0)<CR>
nmap <silent> [a <Plug>(ale_previous_wrap)
nmap <silent> ]a <Plug>(ale_next_wrap)


" Good vacant mappings.
" nnoremap U <nop>
" nnoremap <Del> <nop>
" nnoremap <CR> <nop>
" nnoremap _ <nop>
" nnoremap \| <nop>
" }}}1

" Autocommands {{{1
if has("autocmd")
    augroup FTOptions " {{{2
        autocmd!
        autocmd FileType vim setl fdm=marker sw=4 sts=4 ts=4
        autocmd FileType mail setl wrap
        autocmd FileType gitcommit setl spell

        autocmd FileType python setl et ai nosi sw=4 sts=4 ts=4 fdm=indent
        autocmd FileType html,htmldjango,liquid setl et ai sw=2 sts=2 ts=2
        autocmd FileType css,scss,sass,stylus setl et nosi sw=2 sts=2 ts=2
        autocmd FileType go setl noet sw=4 sts=4 ts=4
        autocmd FileType rust setl et nosi sw=4 sts=4 ts=4
        autocmd FileType haskell setl et sw=4 sts=4 ts=4
        autocmd FileType ruby setl et nosi sw=2 sts=2 ts=2
        autocmd FileType fish setl et sw=4 sts=4 ts=4
    augroup END " }}}2

    augroup Misc " {{{2
        autocmd!
        " autocmd FocusLost * :wa
        autocmd VimResized * exe "normal! \<c-w>="
    augroup END " }}}2

    augroup Whitespace " {{{2
        autocmd!
        " Highlight EOL whitespace, http://vim.wikia.com/wiki/Highlight_unwanted_spaces
        autocmd ColorScheme * highlight ExtraWhitespace ctermbg=darkred guibg=#382424
        autocmd BufWinEnter * match ExtraWhitespace /\s\+$/
        " the above flashes annoyingly while typing, be calmer in insert mode
        autocmd InsertLeave * match ExtraWhitespace /\s\+$/
        autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/
    augroup END " }}}2

    augroup command_window " {{{2
        autocmd!
        " Have <Ctrl-C> leave cmdline-window.
        autocmd CmdwinEnter * nnoremap <buffer> <C-c> :q\|echo ""<cr>
        autocmd CmdwinEnter * inoremap <buffer> <C-c> <esc>:q\|echo ""<cr>
        " Start command line window in insert mode and no line numbers.
        autocmd CmdwinEnter * startinsert
        autocmd CmdwinEnter * setl nonumber
        autocmd BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal g'\"" | endif
    augroup END " }}}2
end
" }}}1

" Commands {{{1

" Visual star {{{
function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>
" }}}

" Fix whitespace {{{
highlight ExtraWhitespace ctermbg=darkred guibg=#382424

function! s:FixWhitespace(line1,line2)
    let l:save_cursor = getpos(".")
    silent! execute ':' . a:line1 . ',' . a:line2 . 's/\s\+$//'
    call setpos('.', l:save_cursor)
endfunction

" Run :FixWhitespace to remove end of line white space.
command! -range=% FixWhitespace call <SID>FixWhitespace(<line1>,<line2>)
" }}}

" }}}

" Abbreviations {{{1
function! EatChar(pat)
    let c = nr2char(getchar(0))
    return (c =~ a:pat) ? '' : c
endfunction

function! MakeSpacelessIabbrev(from, to)
    execute "iabbrev <silent> ".a:from." ".a:to."<C-R>=EatChar('\\s')<CR>"
endfunction

call MakeSpacelessIabbrev('***', '*args, **kwargs')
call MakeSpacelessIabbrev('pdb', 'breakpoint()')

iab wekk week
iab Wekk Week
iab chage change
iab Chage Change
iab reutrn return
iab reutn return
iab reurn return
iab labmda lambda
iab consoel console
" }}}

if filereadable($HOME."/.vim/local/vimrc")
    set runtimepath+=$HOME/.vim/local/
    source $HOME/.vim/local/vimrc
endif
